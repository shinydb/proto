const std = @import("std");
const Type = std.builtin.Type;
const Operation = @import("operation.zig").Operation;
const Status = @import("operation.zig").Status;
const ValueType = @import("operation.zig").ValueType;
const Attribute = @import("operation.zig").Attribute;
const DocType = @import("operation.zig").DocType;
const ArrayList = std.ArrayList;
const Allocator = std.mem.Allocator;
const Buffer = @import("utils").Buffer;

pub const Packet = struct {
    checksum: u64,
    packet_length: u32,
    packet_id: u32,
    timestamp: i64,
    op: Operation,

    pub fn calcMsgSize(pck: *const Packet) usize {
        var size: usize = 0;
        size += @sizeOf(u64); // checksum
        size += @sizeOf(u32); // packet_length
        size += @sizeOf(u32); // packet_id
        size += @sizeOf(i64); // timestamp
        size += 1; // op tag (u8)
        switch (pck.op) {
            .Authenticate => |data| {
                size += 4 + @as(u32, @intCast(data.uid.len));
                size += 4 + @as(u32, @intCast(data.key.len));
            },
            .Logout => {},
            .Insert => |data| {
                size += 4 + @as(u32, @intCast(data.store_ns.len));
                size += 4 + @as(u32, @intCast(data.payload.len));
                size += 1; // auto_create (u8)
            },
            .BatchInsert => |data| {
                size += 4 + @as(u32, @intCast(data.store_ns.len));
                size += 4; // count (u32)
                for (data.values) |value| {
                    size += 4 + @as(u32, @intCast(value.len));
                }
            },
            .Read => |data| {
                size += 4 + @as(u32, @intCast(data.store_ns.len));
                size += 16; // id (u128)
            },
            .Update => |data| {
                size += 4 + @as(u32, @intCast(data.store_ns.len));
                size += 16; // id (u128)
                size += 4 + @as(u32, @intCast(data.payload.len));
            },
            .Delete => |data| {
                size += 4 + @as(u32, @intCast(data.store_ns.len));
                size += 1; // has_id (u8)
                if (data.id) |_| {
                    size += 16; // id (u128)
                }
                size += 1; // has_query_json (u8)
                if (data.query_json) |qj| {
                    size += 4 + @as(u32, @intCast(qj.len));
                }
            },
            .Query => |data| {
                size += 4 + @as(u32, @intCast(data.store_ns.len));
                size += 4 + @as(u32, @intCast(data.query_json.len));
            },
            .Aggregate => |data| {
                size += 4 + @as(u32, @intCast(data.store_ns.len));
                size += 4 + @as(u32, @intCast(data.aggregate_json.len));
            },
            .Scan => |data| {
                size += 4 + @as(u32, @intCast(data.store_ns.len));
                size += 1; // has_start_key (u8)
                if (data.start_key) |_| {
                    size += 16; // start_key (u128)
                }
                size += 4; // limit (u32)
                size += 4; // skip (u32)
            },
            .Range => |data| {
                size += 4 + @as(u32, @intCast(data.store_ns.len));
                size += data.start_key.calcAttributeSize();
                size += data.end_key.calcAttributeSize();
            },
            .List => |data| {
                size += 1; // doc_type (u8)
                size += 1; // has_ns (u8)
                if (data.ns) |ns| {
                    size += 4 + @as(u32, @intCast(ns.len));
                }
                size += 1; // has_limit (u8)
                if (data.limit) |_| {
                    size += 4; // limit (u32)
                }
                size += 1; // has_offset (u8)
                if (data.offset) |_| {
                    size += 4; // offset (u32)
                }
            },
            .Reply => |data| {
                size += 1; // status (u8)
                size += 1; // has_data (u8)
                if (data.data) |d| {
                    size += 4 + @as(u32, @intCast(d.len));
                }
            },
            .BatchReply => |data| {
                size += 1; // status (u8)
                size += 4; // count (u32)
                for (data.results) |result| {
                    size += 4 + @as(u32, @intCast(result.len));
                }
            },
            .Create => |data| {
                size += 1; // doc_type (u8)
                size += 4 + @as(u32, @intCast(data.ns.len));
                size += 4 + @as(u32, @intCast(data.payload.len));
                size += 1; // auto_create (u8)
                size += 1; // has_metadata (u8)
                if (data.metadata) |meta| {
                    size += 4 + @as(u32, @intCast(meta.len));
                }
            },
            .Drop => |data| {
                size += 1; // doc_type (u8)
                size += 4 + @as(u32, @intCast(data.name.len));
            },
            .Flush => {},
        }
        return size;
    }

    pub fn serialize(self: Packet, buf: *Buffer) ![]u8 {
        const w = buf.writer();
        try w.writeInt(u64, self.checksum, .little);
        try w.writeInt(u32, self.packet_length, .little);
        try w.writeInt(u32, self.packet_id, .little);
        try w.writeInt(i64, self.timestamp, .little);
        try Packet.serializeOperation(w, self.op);
        return buf.slice();
    }

    pub fn deserialize(allocator: Allocator, data: []const u8) !Packet {
        var offset: usize = 0;
        const checksum = try Packet.readBytes(data, &offset, u64);
        const packet_length = try Packet.readBytes(data, &offset, u32);
        const packet_id = try Packet.readBytes(data, &offset, u32);
        const timestamp = try Packet.readBytes(data, &offset, i64);
        const op = try Packet.deserializeOperation(allocator, data, &offset);
        return Packet{
            .checksum = checksum,
            .packet_length = packet_length,
            .packet_id = packet_id,
            .timestamp = timestamp,
            .op = op,
        };
    }

    pub fn free(allocator: Allocator, pck: Packet) void {
        switch (pck.op) {
            .BatchInsert => |data| {
                allocator.free(data.values);
            },
            .BatchReply => |data| {
                for (data.results) |result| {
                    allocator.free(result);
                }
                allocator.free(data.results);
            },
            else => {},
        }
    }

    fn serializeAttribute(w: Buffer.Writer, attr: Attribute) !void {
        const tag = @intFromEnum(attr);
        try w.writeInt(u8, tag, .little);
        switch (attr) {
            .I8 => |data| {
                try w.writeString(data.name);
                try w.writeInt(i8, data.value, .little);
            },
            .I16 => |data| {
                try w.writeString(data.name);
                try w.writeInt(i16, data.value, .little);
            },
            .I32 => |data| {
                try w.writeString(data.name);
                try w.writeInt(i32, data.value, .little);
            },
            .I64 => |data| {
                try w.writeString(data.name);
                try w.writeInt(i64, data.value, .little);
            },
            .I128 => |data| {
                try w.writeString(data.name);
                try w.writeInt(i128, data.value, .little);
            },
            .U8 => |data| {
                try w.writeString(data.name);
                try w.writeInt(u8, data.value, .little);
            },
            .U16 => |data| {
                try w.writeString(data.name);
                try w.writeInt(u16, data.value, .little);
            },
            .U32 => |data| {
                try w.writeString(data.name);
                try w.writeInt(u32, data.value, .little);
            },
            .U64 => |data| {
                try w.writeString(data.name);
                try w.writeInt(u64, data.value, .little);
            },
            .U128 => |data| {
                try w.writeString(data.name);
                try w.writeInt(u128, data.value, .little);
            },
            .F32 => |data| {
                try w.writeString(data.name);
                try w.writeInt(i32, @bitCast(data.value), .little);
            },
            .F64 => |data| {
                try w.writeString(data.name);
                try w.writeInt(i64, @bitCast(data.value), .little);
            },
            .F128 => |data| {
                try w.writeString(data.name);
                try w.writeInt(i128, @bitCast(data.value), .little);
            },
            .Pointer => |data| {
                try w.writeString(data.name);
                try w.writeString(data.value);
            },
        }
    }

    fn serializeOperation(w: Buffer.Writer, op: Operation) !void {
        const tag = @intFromEnum(op);
        try w.writeInt(u8, tag, .little);
        switch (op) {
            .Authenticate => |data| {
                try w.writeString(data.uid);
                try w.writeString(data.key);
            },
            .Logout => {},
            .Insert => |data| {
                try w.writeString(data.store_ns);
                try w.writeString(data.payload);
                try w.writeInt(u8, if (data.auto_create) 1 else 0, .little);
            },
            .BatchInsert => |data| {
                try w.writeString(data.store_ns);
                const count = @as(u32, @intCast(data.values.len));
                try w.writeInt(u32, count, .little);
                for (data.values) |value| {
                    try w.writeString(value);
                }
            },
            .Read => |data| {
                try w.writeString(data.store_ns);
                try w.writeInt(u128, data.id, .little);
            },
            .Update => |data| {
                try w.writeString(data.store_ns);
                try w.writeInt(u128, data.id, .little);
                try w.writeString(data.payload);
            },
            .Delete => |data| {
                try w.writeString(data.store_ns);
                if (data.id) |id| {
                    try w.writeInt(u8, 1, .little);
                    try w.writeInt(u128, id, .little);
                } else {
                    try w.writeInt(u8, 0, .little);
                }
                if (data.query_json) |qj| {
                    try w.writeInt(u8, 1, .little);
                    try w.writeString(qj);
                } else {
                    try w.writeInt(u8, 0, .little);
                }
            },
            .Query => |data| {
                try w.writeString(data.store_ns);
                try w.writeString(data.query_json);
            },
            .Aggregate => |data| {
                try w.writeString(data.store_ns);
                try w.writeString(data.aggregate_json);
            },
            .Scan => |data| {
                try w.writeString(data.store_ns);
                if (data.start_key) |key| {
                    try w.writeInt(u8, 1, .little);
                    try w.writeInt(u128, key, .little);
                } else {
                    try w.writeInt(u8, 0, .little);
                }
                try w.writeInt(u32, data.limit, .little);
                try w.writeInt(u32, data.skip, .little);
            },
            .Range => |data| {
                try w.writeString(data.store_ns);
                try serializeAttribute(w, data.start_key);
                try serializeAttribute(w, data.end_key);
            },
            .List => |data| {
                try w.writeInt(u8, @intFromEnum(data.doc_type), .little);
                if (data.ns) |ns| {
                    try w.writeInt(u8, 1, .little);
                    try w.writeString(ns);
                } else {
                    try w.writeInt(u8, 0, .little);
                }
                if (data.limit) |lim| {
                    try w.writeInt(u8, 1, .little);
                    try w.writeInt(u32, lim, .little);
                } else {
                    try w.writeInt(u8, 0, .little);
                }
                if (data.offset) |off| {
                    try w.writeInt(u8, 1, .little);
                    try w.writeInt(u32, off, .little);
                } else {
                    try w.writeInt(u8, 0, .little);
                }
            },
            .Reply => |data| {
                try w.writeInt(u8, @intFromEnum(data.status), .little);
                if (data.data) |d| {
                    try w.writeInt(u8, 1, .little);
                    try w.writeString(d);
                } else {
                    try w.writeInt(u8, 0, .little);
                }
            },
            .BatchReply => |data| {
                try w.writeInt(u8, @intFromEnum(data.status), .little);
                const count = @as(u32, @intCast(data.results.len));
                try w.writeInt(u32, count, .little);
                for (data.results) |result| {
                    try w.writeString(result);
                }
            },
            .Create => |data| {
                try w.writeInt(u8, @intFromEnum(data.doc_type), .little);
                try w.writeString(data.ns);
                try w.writeString(data.payload);
                try w.writeInt(u8, if (data.auto_create) 1 else 0, .little);
                if (data.metadata) |meta| {
                    try w.writeInt(u8, 1, .little);
                    try w.writeString(meta);
                } else {
                    try w.writeInt(u8, 0, .little);
                }
            },
            .Drop => |data| {
                try w.writeInt(u8, @intFromEnum(data.doc_type), .little);
                try w.writeString(data.name);
            },
            .Flush => {},
        }
    }

    fn readBytes(data: []const u8, offset: *usize, comptime T: type) !T {
        if (offset.* + @sizeOf(T) > data.len) {
            return SerializationError.InvalidData;
        }
        const result = std.mem.bytesToValue(T, data[offset.* .. offset.* + @sizeOf(T)]);
        offset.* += @sizeOf(T);
        return result;
    }

    fn readString(allocator: Allocator, data: []const u8, offset: *usize) ![]const u8 {
        const len = try Packet.readBytes(data, offset, u32);
        if (offset.* + len > data.len) {
            return SerializationError.InvalidData;
        }
        _ = allocator;
        const result = data[offset.* .. offset.* + len][0..];
        offset.* += len;
        return result;
    }

    fn deserializeAttribute(allocator: Allocator, data: []const u8, offset: *usize) !Attribute {
        const tag = try Packet.readBytes(data, offset, u8);
        return switch (tag) {
            0 => {
                const name = try Packet.readString(allocator, data, offset);
                const value = try Packet.readBytes(data, offset, i8);
                return Attribute{ .I8 = .{ .name = name, .value = value } };
            },
            1 => {
                const name = try Packet.readString(allocator, data, offset);
                const value = try Packet.readBytes(data, offset, i16);
                return Attribute{ .I16 = .{ .name = name, .value = value } };
            },
            2 => {
                const name = try Packet.readString(allocator, data, offset);
                const value = try Packet.readBytes(data, offset, i32);
                return Attribute{ .I32 = .{ .name = name, .value = value } };
            },
            3 => {
                const name = try Packet.readString(allocator, data, offset);
                const value = try Packet.readBytes(data, offset, i64);
                return Attribute{ .I64 = .{ .name = name, .value = value } };
            },
            4 => {
                const name = try Packet.readString(allocator, data, offset);
                const value = try Packet.readBytes(data, offset, i128);
                return Attribute{ .I128 = .{ .name = name, .value = value } };
            },
            5 => {
                const name = try Packet.readString(allocator, data, offset);
                const value = try Packet.readBytes(data, offset, u8);
                return Attribute{ .U8 = .{ .name = name, .value = value } };
            },
            6 => {
                const name = try Packet.readString(allocator, data, offset);
                const value = try Packet.readBytes(data, offset, u16);
                return Attribute{ .U16 = .{ .name = name, .value = value } };
            },
            7 => {
                const name = try Packet.readString(allocator, data, offset);
                const value = try Packet.readBytes(data, offset, u32);
                return Attribute{ .U32 = .{ .name = name, .value = value } };
            },
            8 => {
                const name = try Packet.readString(allocator, data, offset);
                const value = try Packet.readBytes(data, offset, u64);
                return Attribute{ .U64 = .{ .name = name, .value = value } };
            },
            9 => {
                const name = try Packet.readString(allocator, data, offset);
                const value = try Packet.readBytes(data, offset, u128);
                return Attribute{ .U128 = .{ .name = name, .value = value } };
            },
            10 => {
                const name = try Packet.readString(allocator, data, offset);
                const value = try Packet.readBytes(data, offset, f32);
                return Attribute{ .F32 = .{ .name = name, .value = value } };
            },
            11 => {
                const name = try Packet.readString(allocator, data, offset);
                const value = try Packet.readBytes(data, offset, f64);
                return Attribute{ .F64 = .{ .name = name, .value = value } };
            },
            12 => {
                const name = try Packet.readString(allocator, data, offset);
                const value = try Packet.readBytes(data, offset, f128);
                return Attribute{ .F128 = .{ .name = name, .value = value } };
            },
            13 => {
                const name = try Packet.readString(allocator, data, offset);
                const value = try Packet.readString(allocator, data, offset);
                return Attribute{ .Pointer = .{ .name = name, .value = value } };
            },
            else => SerializationError.InvalidData,
        };
    }

    fn deserializeOperation(allocator: Allocator, data: []const u8, offset: *usize) !Operation {
        const tag = try Packet.readBytes(data, offset, u8);
        return switch (tag) {
            // Tag 1: Authenticate
            1 => {
                const uid = try Packet.readString(allocator, data, offset);
                const key = try Packet.readString(allocator, data, offset);
                return Operation{ .Authenticate = .{ .uid = uid, .key = key } };
            },
            // Tag 2: Logout
            2 => Operation.Logout,
            // Tag 3: Insert
            3 => {
                const store_ns = try Packet.readString(allocator, data, offset);
                const payload = try Packet.readString(allocator, data, offset);
                const auto_create = (try Packet.readBytes(data, offset, u8)) == 1;
                return Operation{ .Insert = .{
                    .store_ns = store_ns,
                    .payload = payload,
                    .auto_create = auto_create,
                } };
            },
            // Tag 4: BatchInsert
            4 => {
                const store_ns = try Packet.readString(allocator, data, offset);
                const count = try Packet.readBytes(data, offset, u32);
                const values = try allocator.alloc([]const u8, count);
                for (values) |*value| {
                    value.* = try Packet.readString(allocator, data, offset);
                }
                return Operation{ .BatchInsert = .{
                    .store_ns = store_ns,
                    .values = values,
                } };
            },
            // Tag 5: Read
            5 => {
                const store_ns = try Packet.readString(allocator, data, offset);
                const id = try Packet.readBytes(data, offset, u128);
                return Operation{ .Read = .{ .store_ns = store_ns, .id = id } };
            },
            // Tag 6: Update
            6 => {
                const store_ns = try Packet.readString(allocator, data, offset);
                const id = try Packet.readBytes(data, offset, u128);
                const payload = try Packet.readString(allocator, data, offset);
                return Operation{ .Update = .{
                    .store_ns = store_ns,
                    .id = id,
                    .payload = payload,
                } };
            },
            // Tag 7: Delete
            7 => {
                const store_ns = try Packet.readString(allocator, data, offset);
                const has_id = try Packet.readBytes(data, offset, u8);
                const id = if (has_id == 1) try Packet.readBytes(data, offset, u128) else null;
                const has_query_json = try Packet.readBytes(data, offset, u8);
                const query_json = if (has_query_json == 1) try Packet.readString(allocator, data, offset) else null;
                return Operation{ .Delete = .{
                    .store_ns = store_ns,
                    .id = id,
                    .query_json = query_json,
                } };
            },
            // Tag 8: Query
            8 => {
                const store_ns = try Packet.readString(allocator, data, offset);
                const query_json = try Packet.readString(allocator, data, offset);
                return Operation{ .Query = .{ .store_ns = store_ns, .query_json = query_json } };
            },
            // Tag 9: Aggregate
            9 => {
                const store_ns = try Packet.readString(allocator, data, offset);
                const aggregate_json = try Packet.readString(allocator, data, offset);
                return Operation{ .Aggregate = .{ .store_ns = store_ns, .aggregate_json = aggregate_json } };
            },
            // Tag 10: Scan
            10 => {
                const store_ns = try Packet.readString(allocator, data, offset);
                const has_start_key = try Packet.readBytes(data, offset, u8);
                const start_key = if (has_start_key == 1) try Packet.readBytes(data, offset, u128) else null;
                const limit = try Packet.readBytes(data, offset, u32);
                const skip = try Packet.readBytes(data, offset, u32);
                return Operation{ .Scan = .{
                    .store_ns = store_ns,
                    .start_key = start_key,
                    .limit = limit,
                    .skip = skip,
                } };
            },
            // Tag 11: Range
            11 => {
                const store_ns = try Packet.readString(allocator, data, offset);
                const start_key = try Packet.deserializeAttribute(allocator, data, offset);
                const end_key = try Packet.deserializeAttribute(allocator, data, offset);
                return Operation{ .Range = .{
                    .store_ns = store_ns,
                    .start_key = start_key,
                    .end_key = end_key,
                } };
            },
            // Tag 12: List
            12 => {
                const doc_type_byte = try Packet.readBytes(data, offset, u8);
                const doc_type = @as(DocType, @enumFromInt(doc_type_byte));
                const has_ns = try Packet.readBytes(data, offset, u8);
                const ns = if (has_ns == 1) try Packet.readString(allocator, data, offset) else null;
                const has_limit = try Packet.readBytes(data, offset, u8);
                const limit = if (has_limit == 1) try Packet.readBytes(data, offset, u32) else null;
                const has_offset = try Packet.readBytes(data, offset, u8);
                const offset_val = if (has_offset == 1) try Packet.readBytes(data, offset, u32) else null;
                return Operation{ .List = .{
                    .doc_type = doc_type,
                    .ns = ns,
                    .limit = limit,
                    .offset = offset_val,
                } };
            },
            // Tag 50: Reply
            50 => {
                const status_byte = try Packet.readBytes(data, offset, u8);
                const status = @as(Status, @enumFromInt(status_byte));
                const has_data = try Packet.readBytes(data, offset, u8);
                const reply_data = if (has_data == 1) try Packet.readString(allocator, data, offset) else null;
                return Operation{ .Reply = .{ .status = status, .data = reply_data } };
            },
            // Tag 51: BatchReply
            51 => {
                const status_byte = try Packet.readBytes(data, offset, u8);
                const status = @as(Status, @enumFromInt(status_byte));
                const count = try Packet.readBytes(data, offset, u32);
                const results = try allocator.alloc([]const u8, count);
                for (results) |*result| {
                    result.* = try Packet.readString(allocator, data, offset);
                }
                return Operation{ .BatchReply = .{ .status = status, .results = results } };
            },

            // Admin operations (subset)
            100 => {
                const doc_type_byte = try Packet.readBytes(data, offset, u8);
                const doc_type = @as(DocType, @enumFromInt(doc_type_byte));
                const ns = try Packet.readString(allocator, data, offset);
                const payload = try Packet.readString(allocator, data, offset);
                const auto_create = (try Packet.readBytes(data, offset, u8)) == 1;
                const has_metadata = try Packet.readBytes(data, offset, u8);
                const metadata = if (has_metadata == 1) try Packet.readString(allocator, data, offset) else null;
                return Operation{ .Create = .{
                    .doc_type = doc_type,
                    .ns = ns,
                    .payload = payload,
                    .auto_create = auto_create,
                    .metadata = metadata,
                } };
            },
            101 => {
                const doc_type_byte = try Packet.readBytes(data, offset, u8);
                const doc_type = @as(DocType, @enumFromInt(doc_type_byte));
                const name = try Packet.readString(allocator, data, offset);
                return Operation{ .Drop = .{
                    .doc_type = doc_type,
                    .name = name,
                } };
            },
            102 => Operation.Flush,

            else => SerializationError.InvalidData,
        };
    }

    fn freeAttribute(allocator: Allocator, attr: Attribute) void {
        switch (attr) {
            .Pointer => |data| {
                allocator.free(data.name);
                allocator.free(data.value);
            },
            .I8 => |data| allocator.free(data.name),
            .I16 => |data| allocator.free(data.name),
            .I32 => |data| allocator.free(data.name),
            .I64 => |data| allocator.free(data.name),
            .I128 => |data| allocator.free(data.name),
            .U8 => |data| allocator.free(data.name),
            .U16 => |data| allocator.free(data.name),
            .U32 => |data| allocator.free(data.name),
            .U64 => |data| allocator.free(data.name),
            .U128 => |data| allocator.free(data.name),
            .F32 => |data| allocator.free(data.name),
            .F64 => |data| allocator.free(data.name),
            .F128 => |data| allocator.free(data.name),
        }
    }
};


pub const SerializationError = error{
    BufferTooSmall,
    InvalidData,
    OutOfMemory,
};

const testing = std.testing;
const expect = testing.expect;
const expectEqual = testing.expectEqual;
const expectEqualStrings = testing.expectEqualStrings;

// ========== Round-trip helper ==========

fn roundTrip(original: Packet) !void {
    const allocator = testing.allocator;
    var buf = try Buffer.init(allocator, original.calcMsgSize());
    defer buf.deinit();
    const serialized = try original.serialize(&buf);
    const deserialized = try Packet.deserialize(allocator, serialized);
    defer Packet.free(allocator, deserialized);
    try expectEqual(original.checksum, deserialized.checksum);
    try expectEqual(original.packet_id, deserialized.packet_id);
    try expectEqual(original.timestamp, deserialized.timestamp);
    try expectEqual(@intFromEnum(original.op), @intFromEnum(deserialized.op));
}

// ========== Tests ==========

test "round-trip Authenticate" {
    const pkt = Packet{
        .checksum = 17,
        .packet_length = 0,
        .packet_id = 26,
        .timestamp = 17000,
        .op = Operation{ .Authenticate = .{ .uid = "admin", .key = "secret_key_123" } },
    };
    try roundTrip(pkt);
    const allocator = testing.allocator;
    var buf = try Buffer.init(allocator, pkt.calcMsgSize());
    defer buf.deinit();
    const serialized = try pkt.serialize(&buf);
    const d = try Packet.deserialize(allocator, serialized);
    defer Packet.free(allocator, d);
    try expectEqualStrings("admin", d.op.Authenticate.uid);
    try expectEqualStrings("secret_key_123", d.op.Authenticate.key);
}

test "round-trip Logout" {
    try roundTrip(Packet{
        .checksum = 19,
        .packet_length = 0,
        .packet_id = 28,
        .timestamp = 19000,
        .op = Operation.Logout,
    });
}

test "round-trip Insert" {
    const pkt = Packet{
        .checksum = 6,
        .packet_length = 0,
        .packet_id = 15,
        .timestamp = 6000,
        .op = Operation{ .Insert = .{ .store_ns = "users", .payload = "\x10\x00\x00\x00", .auto_create = true } },
    };
    try roundTrip(pkt);
    const allocator = testing.allocator;
    var buf = try Buffer.init(allocator, pkt.calcMsgSize());
    defer buf.deinit();
    const serialized = try pkt.serialize(&buf);
    const d = try Packet.deserialize(allocator, serialized);
    defer Packet.free(allocator, d);
    try expectEqualStrings("users", d.op.Insert.store_ns);
    try expect(d.op.Insert.auto_create);
}

test "round-trip BatchInsert" {
    const allocator = testing.allocator;
    const values = try allocator.alloc([]const u8, 3);
    defer allocator.free(values);
    values[0] = "doc1";
    values[1] = "doc2";
    values[2] = "doc3";
    const pkt = Packet{
        .checksum = 7,
        .packet_length = 0,
        .packet_id = 16,
        .timestamp = 7000,
        .op = Operation{ .BatchInsert = .{ .store_ns = "orders", .values = values } },
    };
    var buf = try Buffer.init(allocator, pkt.calcMsgSize());
    defer buf.deinit();
    const serialized = try pkt.serialize(&buf);
    const d = try Packet.deserialize(allocator, serialized);
    defer Packet.free(allocator, d);
    try expectEqual(@as(usize, 3), d.op.BatchInsert.values.len);
    try expectEqualStrings("doc1", d.op.BatchInsert.values[0]);
}

test "round-trip Read" {
    const pkt = Packet{
        .checksum = 8,
        .packet_length = 0,
        .packet_id = 17,
        .timestamp = 8000,
        .op = Operation{ .Read = .{ .store_ns = "users", .id = 0xDEADBEEF_CAFEBABE_12345678_9ABCDEF0 } },
    };
    try roundTrip(pkt);
    const allocator = testing.allocator;
    var buf = try Buffer.init(allocator, pkt.calcMsgSize());
    defer buf.deinit();
    const serialized = try pkt.serialize(&buf);
    const d = try Packet.deserialize(allocator, serialized);
    defer Packet.free(allocator, d);
    try expectEqual(@as(u128, 0xDEADBEEF_CAFEBABE_12345678_9ABCDEF0), d.op.Read.id);
}

test "round-trip Update" {
    const pkt = Packet{
        .checksum = 9,
        .packet_length = 0,
        .packet_id = 18,
        .timestamp = 9000,
        .op = Operation{ .Update = .{ .store_ns = "users", .id = 42, .payload = "updated_data" } },
    };
    try roundTrip(pkt);
    const allocator = testing.allocator;
    var buf = try Buffer.init(allocator, pkt.calcMsgSize());
    defer buf.deinit();
    const serialized = try pkt.serialize(&buf);
    const d = try Packet.deserialize(allocator, serialized);
    defer Packet.free(allocator, d);
    try expectEqual(@as(u128, 42), d.op.Update.id);
    try expectEqualStrings("updated_data", d.op.Update.payload);
}

test "round-trip Delete with id" {
    const pkt = Packet{
        .checksum = 10,
        .packet_length = 0,
        .packet_id = 19,
        .timestamp = 10000,
        .op = Operation{ .Delete = .{ .store_ns = "users", .id = 99, .query_json = null } },
    };
    try roundTrip(pkt);
    const allocator = testing.allocator;
    var buf = try Buffer.init(allocator, pkt.calcMsgSize());
    defer buf.deinit();
    const serialized = try pkt.serialize(&buf);
    const d = try Packet.deserialize(allocator, serialized);
    defer Packet.free(allocator, d);
    try expectEqual(@as(u128, 99), d.op.Delete.id.?);
    try expect(d.op.Delete.query_json == null);
}

test "round-trip Delete with query_json" {
    const pkt = Packet{
        .checksum = 11,
        .packet_length = 0,
        .packet_id = 20,
        .timestamp = 11000,
        .op = Operation{ .Delete = .{ .store_ns = "orders", .id = null, .query_json = "{\"status\":\"cancelled\"}" } },
    };
    try roundTrip(pkt);
    const allocator = testing.allocator;
    var buf = try Buffer.init(allocator, pkt.calcMsgSize());
    defer buf.deinit();
    const serialized = try pkt.serialize(&buf);
    const d = try Packet.deserialize(allocator, serialized);
    defer Packet.free(allocator, d);
    try expect(d.op.Delete.id == null);
    try expectEqualStrings("{\"status\":\"cancelled\"}", d.op.Delete.query_json.?);
}

test "round-trip Query" {
    const pkt = Packet{
        .checksum = 13,
        .packet_length = 0,
        .packet_id = 22,
        .timestamp = 13000,
        .op = Operation{ .Query = .{ .store_ns = "users", .query_json = "{\"age\":{\"$gt\":18}}" } },
    };
    try roundTrip(pkt);
    const allocator = testing.allocator;
    var buf = try Buffer.init(allocator, pkt.calcMsgSize());
    defer buf.deinit();
    const serialized = try pkt.serialize(&buf);
    const d = try Packet.deserialize(allocator, serialized);
    defer Packet.free(allocator, d);
    try expectEqualStrings("{\"age\":{\"$gt\":18}}", d.op.Query.query_json);
}

test "round-trip Aggregate" {
    const pkt = Packet{
        .checksum = 14,
        .packet_length = 0,
        .packet_id = 23,
        .timestamp = 14000,
        .op = Operation{ .Aggregate = .{ .store_ns = "sales", .aggregate_json = "[{\"$sum\":\"amount\"}]" } },
    };
    try roundTrip(pkt);
    const allocator = testing.allocator;
    var buf = try Buffer.init(allocator, pkt.calcMsgSize());
    defer buf.deinit();
    const serialized = try pkt.serialize(&buf);
    const d = try Packet.deserialize(allocator, serialized);
    defer Packet.free(allocator, d);
    try expectEqualStrings("[{\"$sum\":\"amount\"}]", d.op.Aggregate.aggregate_json);
}

test "round-trip Scan with start_key" {
    const pkt = Packet{
        .checksum = 15,
        .packet_length = 0,
        .packet_id = 24,
        .timestamp = 15000,
        .op = Operation{ .Scan = .{ .store_ns = "logs", .start_key = 500, .limit = 100, .skip = 10 } },
    };
    try roundTrip(pkt);
    const allocator = testing.allocator;
    var buf = try Buffer.init(allocator, pkt.calcMsgSize());
    defer buf.deinit();
    const serialized = try pkt.serialize(&buf);
    const d = try Packet.deserialize(allocator, serialized);
    defer Packet.free(allocator, d);
    try expectEqual(@as(u128, 500), d.op.Scan.start_key.?);
    try expectEqual(@as(u32, 100), d.op.Scan.limit);
    try expectEqual(@as(u32, 10), d.op.Scan.skip);
}

test "round-trip Scan without start_key" {
    const pkt = Packet{
        .checksum = 16,
        .packet_length = 0,
        .packet_id = 25,
        .timestamp = 16000,
        .op = Operation{ .Scan = .{ .store_ns = "logs", .start_key = null, .limit = 50, .skip = 0 } },
    };
    try roundTrip(pkt);
    const allocator = testing.allocator;
    var buf = try Buffer.init(allocator, pkt.calcMsgSize());
    defer buf.deinit();
    const serialized = try pkt.serialize(&buf);
    const d = try Packet.deserialize(allocator, serialized);
    defer Packet.free(allocator, d);
    try expect(d.op.Scan.start_key == null);
}

test "round-trip Range with I64 attributes" {
    const pkt = Packet{
        .checksum = 12,
        .packet_length = 0,
        .packet_id = 21,
        .timestamp = 12000,
        .op = Operation{ .Range = .{
            .store_ns = "events",
            .start_key = Attribute{ .I64 = .{ .name = "timestamp", .value = 1000 } },
            .end_key = Attribute{ .I64 = .{ .name = "timestamp", .value = 9999 } },
        } },
    };
    try roundTrip(pkt);
    const allocator = testing.allocator;
    var buf = try Buffer.init(allocator, pkt.calcMsgSize());
    defer buf.deinit();
    const serialized = try pkt.serialize(&buf);
    const d = try Packet.deserialize(allocator, serialized);
    defer Packet.free(allocator, d);
    try expectEqual(@as(i64, 1000), d.op.Range.start_key.I64.value);
    try expectEqual(@as(i64, 9999), d.op.Range.end_key.I64.value);
}

test "round-trip List with optional fields" {
    const pkt = Packet{
        .checksum = 4,
        .packet_length = 0,
        .packet_id = 13,
        .timestamp = 4000,
        .op = Operation{ .List = .{ .doc_type = DocType.Store, .ns = null, .limit = 50, .offset = 10 } },
    };
    try roundTrip(pkt);
    const allocator = testing.allocator;
    var buf = try Buffer.init(allocator, pkt.calcMsgSize());
    defer buf.deinit();
    const serialized = try pkt.serialize(&buf);
    const d = try Packet.deserialize(allocator, serialized);
    defer Packet.free(allocator, d);
    try expectEqual(@as(u32, 50), d.op.List.limit.?);
    try expectEqual(@as(u32, 10), d.op.List.offset.?);
    try expect(d.op.List.ns == null);
}

test "round-trip Reply with data" {
    const pkt = Packet{
        .checksum = 22222,
        .packet_length = 75,
        .packet_id = 4,
        .timestamp = 2222222222,
        .op = Operation{ .Reply = .{ .status = Status.ok, .data = "response data" } },
    };
    try roundTrip(pkt);
    const allocator = testing.allocator;
    var buf = try Buffer.init(allocator, pkt.calcMsgSize());
    defer buf.deinit();
    const serialized = try pkt.serialize(&buf);
    const d = try Packet.deserialize(allocator, serialized);
    defer Packet.free(allocator, d);
    try expectEqual(pkt.op.Reply.status, d.op.Reply.status);
    try expectEqualStrings("response data", d.op.Reply.data.?);
}

test "round-trip Reply without data" {
    const pkt = Packet{
        .checksum = 33333,
        .packet_length = 50,
        .packet_id = 5,
        .timestamp = 3333333333,
        .op = Operation{ .Reply = .{ .status = Status.not_found, .data = null } },
    };
    try roundTrip(pkt);
    const allocator = testing.allocator;
    var buf = try Buffer.init(allocator, pkt.calcMsgSize());
    defer buf.deinit();
    const serialized = try pkt.serialize(&buf);
    const d = try Packet.deserialize(allocator, serialized);
    defer Packet.free(allocator, d);
    try expect(d.op.Reply.data == null);
}

test "round-trip BatchReply" {
    const allocator = testing.allocator;
    const results = try allocator.alloc([]const u8, 2);
    defer allocator.free(results);
    results[0] = "key_abc";
    results[1] = "key_def";
    const pkt = Packet{
        .checksum = 23,
        .packet_length = 0,
        .packet_id = 32,
        .timestamp = 23000,
        .op = Operation{ .BatchReply = .{ .status = Status.ok, .results = results } },
    };
    var buf = try Buffer.init(allocator, pkt.calcMsgSize());
    defer buf.deinit();
    const serialized = try pkt.serialize(&buf);
    const d = try Packet.deserialize(allocator, serialized);
    defer allocator.free(d.op.BatchReply.results);
    try expectEqual(Status.ok, d.op.BatchReply.status);
    try expectEqual(@as(usize, 2), d.op.BatchReply.results.len);
    try expectEqualStrings("key_abc", d.op.BatchReply.results[0]);
}

test "round-trip all attribute types via Range" {
    const allocator = testing.allocator;
    const test_cases = .{
        .{ Attribute{ .I8 = .{ .name = "f", .value = -42 } }, Attribute{ .I8 = .{ .name = "f", .value = 42 } } },
        .{ Attribute{ .I16 = .{ .name = "f", .value = -1000 } }, Attribute{ .I16 = .{ .name = "f", .value = 1000 } } },
        .{ Attribute{ .I32 = .{ .name = "f", .value = -100000 } }, Attribute{ .I32 = .{ .name = "f", .value = 100000 } } },
        .{ Attribute{ .U8 = .{ .name = "f", .value = 0 } }, Attribute{ .U8 = .{ .name = "f", .value = 255 } } },
        .{ Attribute{ .U16 = .{ .name = "f", .value = 0 } }, Attribute{ .U16 = .{ .name = "f", .value = 65535 } } },
        .{ Attribute{ .U32 = .{ .name = "f", .value = 0 } }, Attribute{ .U32 = .{ .name = "f", .value = 4294967295 } } },
        .{ Attribute{ .U64 = .{ .name = "f", .value = 0 } }, Attribute{ .U64 = .{ .name = "f", .value = std.math.maxInt(u64) } } },
        .{ Attribute{ .U128 = .{ .name = "f", .value = 0 } }, Attribute{ .U128 = .{ .name = "f", .value = std.math.maxInt(u128) } } },
        .{ Attribute{ .I128 = .{ .name = "f", .value = std.math.minInt(i128) } }, Attribute{ .I128 = .{ .name = "f", .value = std.math.maxInt(i128) } } },
        .{ Attribute{ .Pointer = .{ .name = "f", .value = "hello" } }, Attribute{ .Pointer = .{ .name = "f", .value = "world" } } },
    };
    inline for (test_cases) |tc| {
        const pkt = Packet{
            .checksum = 0,
            .packet_length = 0,
            .packet_id = 0,
            .timestamp = 0,
            .op = Operation{ .Range = .{ .store_ns = "a.b", .start_key = tc[0], .end_key = tc[1] } },
        };
        var buf = try Buffer.init(allocator, pkt.calcMsgSize());
        defer buf.deinit();
        const serialized = try pkt.serialize(&buf);
        const d = try Packet.deserialize(allocator, serialized);
        defer Packet.free(allocator, d);
        try expectEqual(@intFromEnum(tc[0]), @intFromEnum(d.op.Range.start_key));
        try expectEqual(@intFromEnum(tc[1]), @intFromEnum(d.op.Range.end_key));
    }
}

test "error handling for invalid data" {
    const allocator = testing.allocator;
    const empty_data = [_]u8{};
    try testing.expectError(SerializationError.InvalidData, Packet.deserialize(allocator, &empty_data));
    const partial_data = [_]u8{ 1, 2, 3, 4 };
    try testing.expectError(SerializationError.InvalidData, Packet.deserialize(allocator, &partial_data));
    // Header is 24 bytes; op_tag at offset 24; 255 is an invalid op tag.
    var invalid_op_data = [_]u8{0} ** 25;
    invalid_op_data[24] = 255;
    try testing.expectError(SerializationError.InvalidData, Packet.deserialize(allocator, &invalid_op_data));
}

test "deserialize truncated header" {
    const allocator = testing.allocator;
    var short = [_]u8{0} ** 20;
    try testing.expectError(SerializationError.InvalidData, Packet.deserialize(allocator, &short));
}
